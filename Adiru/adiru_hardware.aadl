package adiru_hardware
public
 
  with AFDX_Properties;
  with ARINC653;
  with memories;
  with processors;
 -- with Buses::CAN;

  ------------------------------------------------------------------
  -----------------------buses--------------------------------------
  ------------------------------------------------------------------
  --bus can extends Buses::CAN::CAN
  --end can;

  --bus can_display extends buses::generic_bus
  --end can_display;

  virtual bus VL
  properties
    AFDX_properties::Allowed_Message_Size => 512 bytes .. 12144 bytes;
    AFDX_properties::Jitter_Max => 500 us;
  end VL;

  virtual bus implementation VL.vl1
  properties
    AFDX_properties::Bandwidth_Allocation_Gap => 32 ms;
    AFDX_properties::Latency_Constraint => 1 ms;
  end VL.vl1;

  virtual bus implementation VL.vl2
  properties
    AFDX_properties::Bandwidth_Allocation_Gap => 8 ms;
    AFDX_properties::Latency_Constraint => 1 ms;
  end VL.vl2;

  

  bus physical_afdx_link
  properties
    AFDX_properties::Channel_Type => Full_Duplex;
    AFDX_properties::Bandwidth => 100 MBytesps;
  end physical_afdx_link;

  bus implementation physical_afdx_link.impl
  subcomponents
    VL1: virtual bus VL.vl1;
    VL2: virtual bus VL.vl2;
   
  end physical_afdx_link.impl;

  device afdx_switch
  features
    afdx_access  : requires bus access physical_afdx_link;
  properties
    AFDX_properties::Switching_Delay => 140us;
    -- internal memorie size
  end afdx_switch;

  ------------------------------------------------------------------
  -----------------------memories-----------------------------------
  ------------------------------------------------------------------

  memory mainmemory extends memories::ROM
  end mainmemory;

  memory memchunk extends memories::ROM
  end memchunk;

  -------------------------------------------------------------------
  ----------------------processor------------------------------------
  -------------------------------------------------------------------

  virtual processor partition_runtime
  properties
    Scheduling_Protocol => (POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL);
  end partition_runtime;

  processor arinckernel extends processors::Generic_CPU
  end arinckernel;

  processor implementation arinckernel.adiru
  subcomponents
    partition_ADR : virtual processor partition_runtime;
    partition_IR : virtual processor partition_runtime;
  properties
    ARINC653::Module_Major_Frame => 50ms;

    ARINC653::Module_Schedule => (
    [Partition => reference (partition_ADR);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;],
    [Partition => reference (partition_IR);
    Duration  => 25 ms;
    Periodic_Processing_Start => true;]
    );
  end arinckernel.adiru;

  processor implementation arinckernel.fms
  subcomponents
    partition_FMS : virtual processor partition_runtime;
  properties
    ARINC653::Module_Major_Frame => 60ms;
    ARINC653::Module_Schedule =>
    (
    [Partition => reference (partition_FMS);
    Duration  => 30 ms;
    Periodic_Processing_Start => true;],
    [Partition => reference (partition_FMS);
    Duration  => 30 ms;
    Periodic_Processing_Start => true;]
    );
  end arinckernel.fms;

  

  

  memory implementation mainmemory.adiru
  subcomponents
    mem_ADR : memory memchunk;
    mem_IR : memory memchunk;
  end mainmemory.adiru;

  memory implementation mainmemory.fms
  subcomponents
    mem_fms : memory memchunk;
  end mainmemory.fms;

 

end adiru_hardware;
